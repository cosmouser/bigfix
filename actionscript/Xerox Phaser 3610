parameter "printerName" = "DESIRED NAME OF PRINTER"
parameter "printerPortname" = "PRINTER_PORT:"
parameter "printerHostname" = "PRINTER.DOMAIN.EDU"
parameter "win7Driver" = "x2gprox"
parameter "win10Driver" = "x2GPROP"
parameter "win7Model" = "Xerox Phaser 3610 PCL6"
parameter "win10Model" = "Xerox Phaser 3610 PS"

if {string value of select "caption from win32_operatingsystem" of wmi starts with "Microsoft Windows 7"}
// download win7 driver zip file
// EDIT THIS LINE
prefetch x2gprox.zip sha1:dcf5670bc3a964dff59e1f2284d6c607cb06ca19 size:19234531 https://URLHERE
endif

if {string value of select "caption from win32_operatingsystem" of wmi starts with "Microsoft Windows 10"}
// download win10 driver zip file
// EDIT THIS LINE
prefetch x2GPROP.zip sha1:26e00ea5b1b4fdfaee9476993862e20b4d1e7fb1 size:19159883 https://URLHERE
endif

// ==== get unzip
prefetch unzip-6.00.exe sha1:84debf12767785cd9b43811022407de7413beb6f size:204800 https://URLHERE

// create the printer port using the fqdn of the printer
wait cscript //nologo "{pathname of windows folder & "\system32\Printing_Admin_Scripts\en-US\prnport.vbs"}" -a -r {parameter "printerPortname"} -h {parameter "printerHostname"}

// Win7
if {string value of select "caption from win32_operatingsystem" of wmi starts with "Microsoft Windows 7"}

// unzip the archives to site download folder
waithidden "__Download\unzip-6.00.exe" -o -qq  "{"__Download\" & parameter "win7Driver" & ".zip"}" -d "__Download"

// add the printer using the printer port and the unzipped .inf file
wait "{pathname of windows folder & "\system32\rundll32"}" printui.dll,PrintUIEntry /if /u /b "{parameter "printerName"}" /f "{pathname of client folder of current site & "\__Download\" & parameter "win7Driver" & "\" & parameter "win7Driver" & ".inf"}" /r {parameter "printerPortname"} /m "{parameter "win7Model"}"

endif

// Win10
if {string value of select "caption from win32_operatingsystem" of wmi starts with "Microsoft Windows 10"}

// unzip the archives to site download folder
waithidden "__Download\unzip-6.00.exe" -o -qq  "{"__Download\" & parameter "win10Driver" & ".zip"}" -d "__Download"

// add the printer using the printer port and the unzipped .inf file
wait "{pathname of windows folder & "\system32\rundll32"}" printui.dll,PrintUIEntry /if /u /b "{parameter "printerName"}" /f "{pathname of client folder of current site & "\__Download\" & parameter "win10Driver" & "\" & parameter "win10Driver" & ".inf"}" /r {parameter "printerPortname"} /m "{parameter "win10Model"}"

endif
